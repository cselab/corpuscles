#!/usr/bin/awk -f

BEGIN {
    me = "count"
    print "static const int TagCount[] = {"
}

{
    tag = $1
    count = $5
    if (!integerp(count))
        count = "TAG_MANY"
    else if (count == 1)
        count = "TAG_ONE"
    else if (count == 2)
        count = "TAG_TWO"
    else if (count == 6)
        count = "TAG_SIX"
    else
        err("unknown count %s for %s", count, tag)
    printf "    %s,\n", count
}

END {
    print "};"
}

function err(a, b, c, d, e)
{
    printf "%s: " a "\n", me, b, c, d, e | "cat >&2"
    exit(2)
}

function integerp(x)
{
    return x ~ /^[1-9][0-9]*$/
}
