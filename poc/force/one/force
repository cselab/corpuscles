. he.util

prog=he.force

usg () {
    cat <<!
$prog FILE.tmpl > FILE.c
!
    exit
}

if test $# -ne 0 && test "$1" = -h; then usg; fi

"$AWK" -v prog=$prog '
function ini() {
    name = get_name(ARGV[1])
    Name = capitalize(name)
}
BEGIN {
    ini()
}

pat("array") {
    p_array()
    next
}

pat("malloc") {
    tprint(Malloc)
    next
}

pat("free") {
    tprint(Free)
    next
}

{
    gsub("%name%", name)
    gsub("%Name%", Name)
    print
}

function p_array(   n, v, i, s, malloc, free) {
    if (NF < 2)
	err("%array needs at least two args")
    n = $1
    for (i = 2; i <= NF; i++) {
	v = $i
	s = append(", ", "*" v, s)
	malloc = append(" ", sprintf("M(%s, %s);", n, v), malloc)
	free = append(" ", sprintf("F(%s);", v), free)
    }
    Free = append("\n", free, Free)
    Malloc = append("\n", malloc, Malloc)
    tprint("real " s ";")
}

function q(s) { return "\"" s "\"" }
function eq(a, b) { return "" a == "" b }
function tprint(s,    n, a, i) {
    n = split(s, a, "\n")
    for (i = 1; i <= n; i++)
	print Tab a[i]
}
function msg(s)  { printf "%s: %s\n", prog, s | "cat >&2" }
function msg0(s) { printf "%s\n", s | "cat >&2" }
function err(s) { msg(s); exit(2) }
function emptyp(s) { return s ~ /^[ \t]*$/ }
function capitalize(s,   n, c, r) {
    n = length(s)
    for (i = 1; i <= n; i++) {
	p = ch(i - 1, s)
	c = ch(i, s)
	if (eq(c, "_"))
	    ;
	else if (i == 1 || eq(p, "_"))
	    r = r toupper(c)
	else
	    r = r c
    }
    return r
}
function ch(i, s) { return substr(s, i, 1) }
function get_name(f) {
    sub(/\..*/, "", f)
    return f
}

function append(sep, v, s) {
    return emptyp(s) ? v : s sep v
}

function pat(p) { # sets Tab
    if (!eq($1, "//%" p)) return 0
    Tab = $0
    sub(/\/.*/, "", Tab)
    sub(/[ \t]*\/\/%/, "")
    sub(/^[^ \t]*/, "")
    return 1
}

' "$@"
