load("nchrpl")$
load("scifac")$
load("format")$
load("cgrind")$
load("stringproc") $

cgrind0(e)::=buildq([e: e, s: gensym()],
  block(
    [s],
    s: make_string_output_stream(),
    with_stdout(s, cgrind(e)),
    s: get_output_stream_string(s),
    s: regex_subst_first("", ";\\n$", s),
    s));

%var%: [] $
cg(v)::=buildq([v: v], (
    push('v, %var%),
    printf(true, "~a = ~a;~%", 'v, cgrind0(v)))) $
cgr(v)::=buildq([v: v],
    printf(true, "SET(~a, ~a);~%", 'v, cgrind0(v)));

frm0 (e):= format(e, %poly(Fb, Fa), %factor, gcfac) $
frm1 (e):= format(e, %poly(ux, uy, wx, wy), %factor, gcfac) $

co(dE, d):=frm0(factor(coeff(dE, diff(d))))         $
matchdeclare(x, all) $
defrule(r_sq0, x^2, sq(x)) $
defrule(r_sq1, 1/x^2, 1/sq(x)) $
rsq(e):=apply1(e, r_sq0, r_sq1) $

declare([bx, cx, cy], constant) $

dx(x, y):= px*x + py*y$
dy(x, y):= qx*x + qy*y$

e: [
  dx(bx, 0) = ux - vx,
  dy(bx, 0) = uy - vy,

  dx(cx, cy) = wx - vx,
  dy(cx, cy) = wy - vy
] $

de: diff(e);
v: [px, py,   qx, qy];
ds: linsolve(de, diff(v));
s : linsolve( e,       v);

M: matrix([px, py], [qx, qy])$
D: M . transpose(M);

I1: determinant(M) - 1; /* Lim */
I2: 1/(2*determinant(M))*(mattrace(D) - 2*determinant(M));
I2: factor(I2);

E: F(I1, I2);
gradef(F(a, b), Fa, Fb) $

dE: subst(ds, factor(diff(E))) $
dE: ratsubst('F,   F(I1, I2), dE) $
dE: expand(dE) $

back(e):=(
  e: subst(0, 'vx, e),
  e: subst(0, 'vy, e),
  e: subst(0, 'uy, e),
  e: subst(0, 'qx, e),
  e);

dvx: rsq(gcfac(back(co(dE, vx))));
dvy: rsq(gcfac(back(co(dE, vy))));

dux: rsq(gcfac(back(co(dE, ux))));
duy: rsq(gcfac(back(co(dE, uy))));

dwx: rsq(gcfac(back(co(dE, wx))));
dwy: rsq(gcfac(back(co(dE, wy))));

px: gcfac(back(frm1(assoc('px, s))));
py: gcfac(back(frm1(assoc('py, s))));
qy: gcfac(back(frm1(assoc('qy, s))));

I1: rsq(gcfac(back(I1)));
I2: rsq(gcfac(back(I2)));

Fa: F1(param, 'I1, 'I2) $
Fb: F2(param, 'I1, 'I2) $

pI1: 'I1 $
pI2: 'I2 $
pA : -bx*cy/2;

with_stdout("body",
  printf(true, "NOT_ZERO(bx);~%"),
  printf(true, "NOT_ZERO(cy);~%"),
  cg(px), cg(py), cg(qy),
  cg(I1), cg(I2),
  cg(Fa), cg(Fb),
  newline(),
  cgr(dvx), cgr(dvy), cgr(dux), cgr(duy), cgr(dwx), cgr(dwy),
  cgr(pI1), cgr(pI2), cgr(pA)) $

%var%: reverse(%var%)$
with_stdout("var",
  for e in %var% do printf(true, "~a~%", e));

pfun(e)::=printf(true, "real (*~a)(void*, real, real)~%", e);
real(e)::=printf(true, "~a ~a~%",   'real, e);
preal(e)::=printf(true, "~a *~a~%",  'real, e);
pvoid(e)::=printf(true, "~a *~a~%", 'void, e);
with_stdout("param",
  pvoid(param),
  pfun(F1), pfun(F2),
  real(bx), real(cx), real(cy),
  real(ux), real(wx), real(wy),
  preal(dvx), preal(dvy),
  preal(dux), preal(duy),
  preal(dwx), preal(dwy),
  preal(pI1), preal(pI2), preal(pA));
