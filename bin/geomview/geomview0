. he.util

prog=he.geomview0

usg () {
    msg "$prog"
    msg 'he geomview module'
    exit
}

oogl="$1"; shift

if test $# -ne 0 && test "$1" = -h; then usg; fi

"$AWK" -v prog="$prog" -v oogl="$oogl" -v LOG="$LOG" '
function keys(s,   a, n) {
    n = sub(/^\(/, "", s)
    if (n != 1) return ERR
    n = sub(/\)$/, "", s)
    if (n != 1) return ERR
    n = split(s, a)
    if (a[1] == "rawevent")
       key(a[2])
    return OK
}

function key(k) {
    if      (k == KEY_Q) g("exit")
    else if (k == KEY_P) g("ui-panel geomview on")
    else if (k == KEY_S) {
	msg("write snap.ppm")
	g("snapshot Camera snap.ppm")
    } else if (k == KEY_SPACE) {
        msg("space")
	local()
	g("xform-set `worldgeom` transform {" m2str(A) "}")
    }
    else err("unknown key: " k)
}

function ini() {
    X = 0; Y = 1; Z = 2
    KEY_Q = 113; KEY_S = 115; KEY_P = 112; KEY_SPACE = 32
    OK = 0; EOF = 1; ERR = -1
}

function reg_key(k) { g(sprintf("interest (rawevent %s)", k)) }
function geom() {
    g0("(progn")
	g(sprintf("geometry obj < %s", oogl))
	g("bbox-draw obj no")
    g0(")")
    reg_key(KEY_Q); reg_key(KEY_S); reg_key(KEY_P); reg_key(KEY_SPACE)
}

function quote(s) { sub(/`/, "\"", s); sub(/`/, "\"", s); return s }
function g(s) { g0("(" s ")") }
function g0(s) {
    if ("" LOG != "0") msg("gcl: " quote(s))
    printf "%s\n", quote(s) | "cat"
    close("cat")
}


function local(  T) {
    m_ident(A)
    m_rotate(0.1, 0, 0, A)
    m_translate(0, 0, 0, A)
}

function m_ident(A, i, j) {
    for (i = 0; i < 4; i++)
	for (j = 0; j < 4; j++)
	    A[i,j] = (i == j) + 0
}

function m2str(A,   i, j, s) {
    for (i = 0; (i, 0) in A; i++) {
	if (i > 0) s = s "\n"
	for (j = 0; (i, j) in A; j++) {
	    if (j > 0) s = s " "
	    s = s sprintf("%.16g", A[i,j])
	}
    }
    return s
}

function m_mult(A, B,   i, j, k, C) { # A *= B, c[i,k] = a[i,j] * b[j, k]
    for (i = 0; (i, 0) in A; i++)
	for (j = 0; (i, j) in A; j++)
	    for (k = 0; (j, k) in B; k++)
		C[i,k] += A[i,j] * B[j,k]
    m_copy(C, A)
}

function m_copy(A, B,  k) { for (k in A) B[k] = A[k] }
function m_translate(x, y, z, A) { A[3,X] += x; A[3,Y] += y; A[3,Z] += z }
function m_rotate(x, y, z, A) { m_rotx(x, A); m_roty(y, A); m_rotz(z, A) }
function m_rotx(p, A,  T) { m_rot0(p, T, Y, Z); m_mult(A, T) }
function m_roty(p, A,  T) { m_rot0(p, T, Z, X); m_mult(A, T) }
function m_rotz(p, A,  T) { m_rot0(p, T, X, Y); m_mult(A, T) }
function m_rot0(p, A, P, Q,   c, s) {
    c = cos(p); s = sin(p)
    m_ident(A)
    A[P, P] = c; A[P, Q] = -s
    A[Q, P] = s; A[Q, Q] =  c
}


BEGIN {
    ini()
    geom()
    for (;;) {
	status = getline s
	if (status == ERR) break
	if (keys(s) != OK) {
	    msg("fail to parse: " s)
	    break
	}
    }
}
function msg(s) { printf "%s: %s\n", prog, s | "cat >&2" }
function err(s) { msg(s); g("exit") }
' "$@"
