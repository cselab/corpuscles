#!/bin/sh

. co.util
prog=co.fmt

if test $# -ne 1
then err 'wrong number of arguments'
fi

"$AWK" -v suffix="$1" '
BEGIN {
    RS = ""
    FS = "\n"

    inc["s.o"] = "s"
    inc["d.o"] = "d"
    inc["l.o"] = "l"
}

{
    write($1)
}

function write(f)
{
    if (!sub(/\.c$/, "." suffix, f) &&
        !sub(/\.c\.m4$/, "." suffix, f)) {
        print "wrong suffix " f | "cat >&2"
        exit(2)
    }
    
    sub(/^.\//, "", f)
    print f ":\\"
    for (i = 2; i <= NF; i++)
        print transform($i) "\\"
    print ""
}

function transform(f)
{
    if (eq(f, "real.h"))
        return "co/prec/" inc[suffix] "/real.h"
    else if (eq(f, "real.inc"))
        return "inc/" inc[suffix] "/real.inc"
    else
        return f
}

function eq(a, b)
{
    return "" a == "" b
}
'
